apply plugin: 'com.android.application'
apply plugin: 'me.ele.amigo'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "me.ele.demo"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    signingConfigs {
        release {
            storeFile file("${project.projectDir}/debug.keystore")
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }
    buildTypes {
        release {
            debuggable true
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            minifyEnabled false
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:multidex:1.0.1'
}


def getPkg() {
    return android.defaultConfig.applicationId
}

def getApk() {
    return "app/build/outputs/apk/app-debug.apk"
}

def inheritIO(final InputStream src, final PrintStream dest) {
    new Thread(new Runnable() {
        public void run() {
            Scanner sc = new Scanner(src);
            while (sc.hasNextLine()) {
                dest.println(sc.nextLine());
            }
        }
    }).start();
}

def runCmd(String cmd) {
    println "start cmd: ${cmd}"
    Process p = cmd.execute()
    inheritIO(p.getInputStream(), System.out);
    inheritIO(p.getErrorStream(), System.err);
    p.waitFor()
    println "finish cmd: ${cmd}"
}

def launch() {
    runCmd("adb shell monkey -p ${getPkg()} -c android.intent.category.LAUNCHER 1")
}

def deleteDemoApk() {
    runCmd("adb shell rm -rf /sdcard/demo.apk")
}

task runHost(dependsOn: assembleDebug) << {
    deleteDemoApk()
    runCmd("adb uninstall ${getPkg()}")
    runCmd("adb install -r -d ${getApk()}")
    launch()
}

task runPatch(dependsOn: assembleDebug) << {
    deleteDemoApk()
    runCmd("adb push ${getApk()} /sdcard/demo.apk")
    launch()
}
