buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

repositories {
    jcenter()
}

apply plugin: 'groovy'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.tools.build:gradle:1.5.0'
}

// replace self clean task with empty one
project.afterEvaluate {
    project.tasks.remove(project.clean)
    project.tasks.create('clean') {

    }
}

def GROUP = 'me.ele'
def ARTIFACTID = 'amigo'

group = GROUP
Properties gradleProperties = new Properties()
gradleProperties.load(new File(project.projectDir, '../gradle.properties').newDataInputStream())
version = gradleProperties.getProperty('VERSION')

install {
    repositories.mavenInstaller {
        pom.project {
            name ARTIFACTID
            description 'A hotfix library for Android platform'
            url 'https://github.com/eleme/Amigo'
            inceptionYear '2016'

            packaging 'jar'
            groupId GROUP
            artifactId ARTIFACTID

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }

            }
            developers {
                developer {
                    id "jack_1900"
                    name "Jack Cho"
                    email "jack_1900@163.com"
                }
            }
        }
    }
}

try {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintray {
        user = properties.getProperty("bintrayUser")
        key = properties.getProperty("bintrayApiKey")
        configurations = ['archives']
        pkg {
            repo = 'maven'
            name = ARTIFACTID
            userOrg = user
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/eleme/Amigo'
        }
    }
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }
} catch (Exception e) {
    println e.toString()
}

// configure local maven, release key, and the like
try {
    apply from: "${project.projectDir}/mybuild.gradle"
} catch (Exception e) {
    println e.toString()
}